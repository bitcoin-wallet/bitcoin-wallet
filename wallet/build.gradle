apply plugin: 'com.android.application'
apply plugin: 'androidsvgdrawable'

repositories {
    mavenLocal()
}
configurations {
    all*.exclude group: 'com.google.android', module: 'android'
    all*.exclude group: 'androidx.legacy', module: 'legacy-support-core-ui'
    all*.exclude group: 'androidx.legacy', module: 'legacy-support-core-utils'
}

dependencies {
    compile project(':integration-android')
    compile 'androidx.annotation:annotation:1.0.2'
    compile 'androidx.core:core:1.0.2'
    compile 'androidx.fragment:fragment:1.0.0'
    compile 'androidx.recyclerview:recyclerview:1.0.0'
    compile 'androidx.cardview:cardview:1.0.0'
    compile 'androidx.coordinatorlayout:coordinatorlayout:1.0.0'
    compile 'androidx.viewpager:viewpager:1.0.0'
    compile 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    compile 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    compile "androidx.sqlite:sqlite:2.0.1"
    compile "androidx.room:room-runtime:2.0.0"
    annotationProcessor "androidx.room:room-compiler:2.0.0"
    compile 'org.groestlcoinj:groestlcoinj-core:0.15.2'
    compile 'com.google.protobuf:protobuf-java:3.7.1'
    compile 'com.google.guava:guava:27.1-android'
    compile 'com.google.zxing:core:3.3.3'
    compile 'com.squareup.okhttp3:okhttp:3.12.3'
    compile 'com.squareup.okhttp3:logging-interceptor:3.12.3'
    compile 'com.squareup.moshi:moshi:1.6.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.github.tony19:logback-android:1.3.0-2'
    testCompile 'junit:junit:4.12'
}

ext {
    archivesBaseName = 'groestlcoin-wallet'
}

android {
    compileSdkVersion 'android-28'
    buildToolsVersion '28.0.3'

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId 'de.schildbach.wallet_test'
        generatedDensities = ['hdpi', 'xhdpi']

        dexOptions {
            preDexLibraries false
        }
        // Enabling multidex support.
        multiDexEnabled true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        externalNativeBuild {
            cmake {
                cFlags '-DHAVE_CONFIG_H'
            }
        }
        minSdkVersion 21
    }

    buildTypes {
        all {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard.cfg'
        }
        debug {
            debuggable true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        test {
            java.srcDirs = ['test']
            resources.srcDirs = ['test']
        }
        _testNet3 {
            res.srcDirs = ["testNet3/res"]
        }
        beta {
            res.srcDirs = ["beta/res"]
        }
    }
    flavorDimensions("version")
    productFlavors {
        beta {
            dimension "version"
            applicationId = "hashengineering.groestlcoin.wallet.beta"
        }
        prod {
            dimension "version"
            applicationId = "hashengineering.groestlcoin.wallet"
        }
        _testNet3 {
            dimension "version"
            applicationId = "hashengineering.groestlcoin.wallet_test"
        }
    }

    externalNativeBuild {
        cmake {
            path 'cpp/CMakeLists.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/*.version'
        exclude 'META-INF/proguard/**'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'okhttp3/internal/publicsuffix/publicsuffixes.gz'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
    }
}

task svgToPngMipmap(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics/mipmap')
    to = file('res')
    targetedDensities = ['hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}

task svgToPngMipmapTestnet(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics/mipmap-testnet')
    to = file('testNet3/res')
    targetedDensities = ['hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}

clean {
    file('libs').deleteDir()

}