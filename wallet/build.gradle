apply plugin: 'com.android.application'
apply plugin: 'androidsvgdrawable'

configurations {
    all*.exclude group: 'com.google.android', module: 'android'
    all*.exclude group: 'androidx.legacy', module: 'legacy-support-core-ui'
    all*.exclude group: 'androidx.legacy', module: 'legacy-support-core-utils'
}

dependencies {
    compile project(':integration-android')
    compile 'androidx.annotation:annotation:1.1.0'
    compile 'androidx.core:core:1.1.0'
    compile 'androidx.fragment:fragment:1.1.0'
    compile 'androidx.recyclerview:recyclerview:1.0.0'
    compile 'androidx.cardview:cardview:1.0.0'
    compile 'androidx.coordinatorlayout:coordinatorlayout:1.0.0'
    compile 'androidx.viewpager:viewpager:1.0.0'
    compile 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    compile 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    compile "androidx.sqlite:sqlite:2.0.1"
    compile "androidx.room:room-runtime:2.1.0"
    annotationProcessor "androidx.room:room-compiler:2.1.0"
    compile 'org.bitcoinj:bitcoinj-core:0.15.4'
    compile 'com.google.protobuf:protobuf-java:3.7.1'
    compile 'com.google.guava:guava:28.1-android'
    compile 'com.google.zxing:core:3.3.3'
    compile 'com.squareup.okhttp3:okhttp:3.12.3'
    compile 'com.squareup.okhttp3:logging-interceptor:3.12.3'
    compile 'com.squareup.moshi:moshi:1.6.0'
    compile 'org.slf4j:slf4j-api:1.7.28'
    compile 'com.github.tony19:logback-android:2.0.0'
    compile 'com.android.support:appcompat-v7:23.2.0'
    testCompile 'junit:junit:4.12'
}

ext {
    archivesBaseName = 'bitcoin-wallet'
}

android {
    compileSdkVersion 'android-28'
    buildToolsVersion '28.0.3'

    defaultConfig {
      vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId 'de.schildbach.wallet_test'
        generatedDensities = ['hdpi', 'xhdpi']

        dexOptions {
            preDexLibraries false
        }
    }

    buildTypes {
        all {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard.cfg'
        }
        debug {
            debuggable true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test {
            java.srcDirs = ['test']
            resources.srcDirs = ['test']
        }
    }

    packagingOptions {
        exclude 'META-INF/*.version'
        exclude 'META-INF/proguard/**'
        exclude 'okhttp3/internal/publicsuffix/publicsuffixes.gz'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
    }
}

task svgToPngMipmap(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics/mipmap')
    to = file('res')
    targetedDensities = ['hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}
